plugins {
    id("com.android.library")
    id("maven-publish")
    id("signing")
}

android {
    compileSdk = 34
    buildToolsVersion = "34.0.0"
    ndkVersion = "26.1.10909125"
    namespace = "io.github.vvb2060.ndk.curl"
    defaultConfig {
        minSdk = 21
        targetSdk = 34
        externalNativeBuild {
            ndkBuild {
                targets += "curl_static"
                targets += "nghttp2_static"
                targets += "nghttp3_static"
                targets += "ngtcp2_crypto_static"
                targets += "ngtcp2_static"
                arguments += "-j${Runtime.runtime.availableProcessors()}"
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = false
        prefab = true
        prefabPublishing = true
    }

    externalNativeBuild {
        ndkBuild.path = "src/main/native/Android.mk"
    }

    prefab {
        curl_static {
            headers = "src/main/native/curl/include"
        }
        nghttp2_static {
            headers = "src/main/native/nghttp2/lib/includes"
        }
        ngtcp2_static {
            headers = "src/main/native/ngtcp2/lib/includes"
        }
        ngtcp2_crypto_static {
            headers = "src/main/native/ngtcp2/crypto/includes"
        }
        nghttp3_static {
            headers = "src/main/native/nghttp3/lib/includes"
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    api("io.github.vvb2060.ndk:boringssl:4.0")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            group = "io.github.vvb2060.ndk"
            artifactId = "curl"
            version = "8.6.0"
            afterEvaluate {
                from(components.release)
            }
            pom {
                name = "curl"
                description = "curl static library prefab for android."
                url = 'https://github.com/vvb2060/curl-android'
                licenses {
                    license {
                        name = "curl license"
                        url = 'https://curl.se/docs/copyright.html'
                    }
                }
                developers {
                    developer {
                        name = "vvb2060"
                        url = "https://github.com/vvb2060"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/vvb2060/curl-android.git"
                    url = "https://github.com/vvb2060/curl-android"
                }
            }
        }
    }
    repositories {
        maven {
            name = "ossrh"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials(PasswordCredentials)
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    def secretKeyRingFile = findProperty("signing.secretKeyRingFile")

    if (secretKeyRingFile != null && file(secretKeyRingFile).exists()) {
        sign(publishing.publications)
    } else if (signingKey != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign(publishing.publications)
    }
}
