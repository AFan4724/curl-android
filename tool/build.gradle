plugins {
    id("com.android.application")
}

android {
    compileSdk = 35
    buildToolsVersion = "35.0.0"
    ndkVersion = "27.0.11902837"
    namespace = "io.github.vvb2060.ndk.curl"
    defaultConfig {
        applicationId = "io.github.vvb2060.ndk.curl"
        minSdk = 21
        targetSdk = 35
        versionCode = 1
        versionName = "1.0"
        externalNativeBuild {
            ndkBuild {
                arguments += [ "-j${Runtime.runtime.availableProcessors()}" ]
                abiFilters += [ "x86", "x86_64", "armeabi-v7a", "arm64-v8a", "riscv64" ]
            }
        }
        ndk {
            debugSymbolLevel = "FULL"
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            shrinkResources = true
            vcsInfo.include = false
            signingConfig = signingConfigs.debug
            proguardFiles("proguard-rules.pro")
            externalNativeBuild {
                ndkBuild {
                    arguments += [ "enableLTO=1" ]
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig = false
        prefab = true
    }

    externalNativeBuild {
        ndkBuild.path = '../curl/src/main/native/Android.mk'
    }

    lint {
        checkReleaseBuilds = false
    }

    dependenciesInfo {
        includeInApk = false
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging(false)
        }
    }
}

dependencies {
    releaseImplementation("io.github.vvb2060.ndk:boringssl:4.1-lto-ndk27")
    debugImplementation("io.github.vvb2060.ndk:boringssl:4.1")
}
